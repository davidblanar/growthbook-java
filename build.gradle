plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "co.uzzu.dotenv.gradle" version "2.0.0"
}

group 'io.github.davidblanar'
archivesBaseName = "growthbook"
version '0.1.0'
//version '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc.options.addStringOption('Xdoclint:none', '-quiet')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'growthbook'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'io.github.davidblanar:growthbook'
                description = 'Growth Book Java SDK'
                url = 'https://github.com/davidblanar/growthbook-java'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://github.com/davidblanar/growthbook-java/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'davidblanar'
                        name = 'David Blan√°r'
                        email = 'david.blanar@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/davidblanar/growthbook-java.git'
                    developerConnection = 'scm:git:ssh://github.com/davidblanar/growthbook-java.git'
                    url = 'https://github.com/davidblanar/growthbook-java'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = env.MAVEN_USERNAME.value
                password = env.MAVEN_PASSWORD.value
            }
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

test {
    useJUnitPlatform()
}